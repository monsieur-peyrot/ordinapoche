# FACTORIELLE

# Ce programme attend un nombre sur le ruban d'entree
# Il renvoie la factorielle de ce nombre sur le ruban de sortie
# (Le nombre d'entrée maximum est donc 6 si on veut éviter un dépassement)
# L'adresse d'execution est 10
# L'adresse 00 doit contenir la valeur 000, l'adresse 01 la valeur 001
# Le programme utilise les adresses memoires 02, 03, 04, 60, 61 et 62
# L'adresse 02 contient le nombre N dont on veut la factorielle
# L'adresse 03 contient le résultat du calcul
# L'adresse 04 contient un compteur qui va de 1 à N
# Pour le sous-programme MULTIPLICATION :
# L'adresse 60 contient le premier nombre à multiplier
# L'adresse 61 contient le deuxième nombre à multiplier
# L'adresse 62 contient le résultat de la multiplication

# DATAS
ADR 00
VAL 000
VAL 001

# PROG PRINCIPAL
ADR 10
INP 02
# On initialise le résultat et le compteur à 1
CLA 01
STO 03
STO 04
# Adresse 14 : on met le résultat actuel en 60 (premier nombre à multiplier)
CLA 03
STO 60
# On met le compteur en 61 (deuxième nombre à multiplier)
CLA 04
STO 61
# On appelle le sous-programme MULTIPLICATION
JMP 40
# Adresse 19 : on stocke le résultat de la multiplication dans le résultat
CLA 62
STO 03
# On incrémente de 1 le compteur
CLA 04
ADD 01
STO 04
# On calcule N - compteur
CLA 02
SUB 04
# Si le résultat est négatif, on saute à l'adresse 28
TAC 28
# Sinon, on recommence à l'adresse 14
JMP 14
# Adresse 28 : on écrit le résultat sur le ruban de sortie
OUT 03
HRS

# SOUS-PROGRAMME MULTIPLICATION
ADR 40
# On met le résultat de la multiplication (62) à zéro
CLA 00
STO 62
# On diminue de 1 le multiplicateur (61)
CLA 61
SUB 01
STO 61
# Adresse 45 : On ajoute le multiplicande (60) au résultat de la multiplication (62)
CLA 62
ADD 60
STO 62
# On diminue de 1 le multiplicateur (61)
CLA 61
SUB 01
# Si le résultat est négatif, on saute à l'adresse 53
TAC 53
# Sinon on stocke la nouvelle valeur du multiplicateur
STO 61
# et on recommence à l'adresse 45
JMP 45
# On retourne au programme principal, à l'adresse 19
JMP 19